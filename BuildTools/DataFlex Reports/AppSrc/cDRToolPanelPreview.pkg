Use Windows.pkg
Use DRLanguage.Inc
Use cDRCommandBarSystem.pkg
Use cDRMenuItem.pkg
Use cDRZoomMenuItem.pkg
Use cDRCurrentPageMenuItem.pkg
Use cDRExportMenuItem.pkg
Use cDRPrintMenuItem.pkg
Use cDRFirstPageMenuItem.pkg
Use cDRPreviousPageMenuItem.pkg
Use cDRNextPageMenuItem.pkg
Use cDRLastPageMenuItem.pkg
Use cDRRefreshMenuItem.pkg
Use cDRReport.pkg
Use DRExportOptions.dg
Use cDRPreviewWindow.pkg
Use cDRSearchPreviewMenuItem.pkg
Use cDRSearchPreviewOptionsMenuItem.pkg

{ ClassLibrary = Windows }
Class cDRToolPanelPreview is a ToolPanel
    Procedure Construct_Object
        Forward Send Construct_Object

        { DesignTime = False }
        { Description = "Will be used to store the object id of the cDRReport" }
        Property Handle phoReport 0

        Set Size to 273 448
        Set Icon to "DR.Ico"
        Set Name to "oDRToolPanelPreview"

        Object oDRPreviewer is a cDRPreviewWindow
            Set Size to 231 440
            Set Location to 18 4
            Set peAnchors to anAll
        End_Object

        Object oCloseButton is a Button
            Set Label to C_$Close
            Set Location to 254 394
            Set peAnchors to anBottomRight
            Set Message 0 to Close_Panel
        End_Object

        Object oCommandBars is a cDRCommandBarSystem
            Set pbAutoResizeIcons to True

            Object oDRToolBar is a cCJToolbar
                Set pbCloseable to False
                Set pbEnableDocking to False
                Set pbGripper to False
                Set peStretched to stStretch
                Set pbDockNextTo to False
                Set pbHideWrap to True
                Set pbCustomizable to False

                Object oFirstPage is a cDRFirstPageMenuItem
                End_Object

                Object oPreviousPage is a cDRPreviousPageMenuItem
                End_Object

                Object oCurrentPage is a cDRCurrentPageMenuItem
                    Set peControlType to xtpControlEdit
                End_Object

                Object oNextPage is a cDRNextPageMenuItem
                End_Object

                Object oLastPage is a cDRLastPageMenuItem
                End_Object

                Object oRefresh is a cDRRefreshMenuItem
                End_Object

                Object oPrint is a cDRPrintMenuItem
                End_Object

                Object oZoom is a cDRZoomMenuItem
                End_Object

                Object oExport is a cDRExportMenuItem
                    Set phoExportOptionsDialog to (oDRExportOptionsDialog)
                End_Object

                Object oSearchInPreview is a cDRSearchPreviewMenuItem
                End_Object

                Object oSearchInPreviewOptions is a cDRMenuItem
                    Set peControlType to xtpControlSplitButtonPopup
                    Set psCaption to C_$DRSearchInPreviewOptions
                    Set psTooltip to C_$DRSearchInPreviewOptionsTooltip
                    Set peControlStyle to xtpButtonIconAndCaption
                    Set psImage to "DRFind.Ico"

                    Object oDownMenuItem is a cDRSearchPreviewOptionsMenuItem
                        Set psCaption to C_$DRSearchInPreviewDown
                        Set phmSearchOptionGet to (RefFunc (pbDown))
                        Set phmSearchOptionSet to (RefProcSet (pbDown))
                        Set phoSearchMenuItem to oSearchInPreview
                    End_Object

                    Object oWholeWordMenuItem is a cDRSearchPreviewOptionsMenuItem
                        Set psCaption to C_$DRSearchInPreviewWholeWord
                        Set phmSearchOptionGet to (RefFunc (pbWholeWord))
                        Set phmSearchOptionSet to (RefProcSet (pbWholeWord))
                        Set phoSearchMenuItem to oSearchInPreview
                    End_Object

                    Object oMatchCaseMenuItem is a cDRSearchPreviewOptionsMenuItem
                        Set psCaption to C_$DRSearchInPreviewMatchCase
                        Set phmSearchOptionGet to (RefFunc (pbMatchCase))
                        Set phmSearchOptionSet to (RefProcSet (pbMatchCase))
                        Set phoSearchMenuItem to oSearchInPreview
                    End_Object
                End_Object
            End_Object
        End_Object

        On_Key Key_Escape Send KeyAction of oCloseButton
    End_Procedure

    // Same method can be used by preview view or toolpanel
    { Visibility = Private }
    Procedure Activate_View
        Send Activate
    End_Procedure

    { Visibility = Private }
    Procedure AssignPreviewHWND
        Handle hoReport hWnd
        String sReportId

        Get Window_Handle of oDRPreviewer to hWnd
        If (hWnd <> 0) Begin
            Get phoReport to hoReport
            If (hoReport <> 0) Begin
                Set PreviewWindowHandle of hoReport to hWnd
            End
        End
    End_Procedure

    { Visibility = Private }
	Procedure PreviewClick Integer wParam Integer lParam
        Handle hoReport

        Get phoReport to hoReport
        If (hoReport <> 0) Begin
            Send PreviewClick of hoReport wParam lParam
        End
    End_Procedure

    { Visibility = Private }
    Procedure PreviewNextPage
        Handle hoReport

        Get phoReport to hoReport
        If (hoReport <> 0) Begin
            Send PreviewNextPage of hoReport
        End
    End_Procedure

    { Visibility = Private }
    Procedure PreviewPreviousPage
        Handle hoReport

        Get phoReport to hoReport
        If (hoReport <> 0) Begin
            Send PreviewPreviousPage of hoReport
        End
    End_Procedure

    { Visibility = Private }
    Procedure PreviewFirstPage
        Handle hoReport

        Get phoReport to hoReport
        If (hoReport <> 0) Begin
            Send PreviewFirstPage of hoReport
        End
    End_Procedure

    { Visibility = Private }
    Procedure PreviewLastPage
        Handle hoReport

        Get phoReport to hoReport
        If (hoReport <> 0) Begin
            Send PreviewLastPage of hoReport
        End
    End_Procedure

    { Visibility = Private }
    Procedure PreviewPaint
        Handle hoReport

        Get phoReport to hoReport
        If (hoReport <> 0) Begin
            Send PreviewPaint of hoReport
        End
    End_Procedure

	{ MethodType = Property }
    { Visibility = Private }
    Function piPreviewZoom Returns Integer
        Handle hoReport
        Integer iFactor

        Get phoReport to hoReport
        If (hoReport <> 0) Begin
            Get piPreviewZoom of hoReport to iFactor
            Function_Return iFactor
        End
    End_Function

	{ MethodType = Property }
    { Visibility = Private }
    Procedure Set piPreviewZoom Integer iFactor
        Handle hoReport

        Get phoReport to hoReport
        If (hoReport <> 0) Begin
            Set piPreviewZoom of hoReport to iFactor
        End
    End_Procedure

    { Visibility = Private }
    Procedure PreviewVScroll Integer iType Integer iPos
        Handle hoReport

        Get phoReport to hoReport
        If (hoReport <> 0) Begin
            Send PreviewVScroll of hoReport iType iPos
        End
    End_Procedure

    { Visibility = Private }
    Procedure PreviewHScroll Integer iType Integer iPos
        Handle hoReport

        Get phoReport to hoReport
        If (hoReport <> 0) Begin
            Send PreviewHScroll of hoReport iType iPos
        End
    End_Procedure

    { MethodType = Event }
    Procedure OnPageChanged Integer iPage
        Set psText of (oCurrentPage (oDRToolBar (oCommandBars))) to (String (iPage))
    End_Procedure

    { MethodType = Event }
    Procedure OnZoomChanged Integer iFactor
        Set psText of (oZoom (oDRToolBar (oCommandBars))) to (String (iFactor) + "%")
    End_Procedure

    { Visibility = Private }
    Procedure Add_Focus Handle hoParent Returns Integer
        Forward Send Add_Focus hoParent

        Send AdjustPanelSize
        Send UpdateViewer
    End_Procedure

    { Visibility = Private }
    Procedure AdjustPanelSize
        Handle hoMain hWnd hoClient
        Integer iLocation iPanelSize

        Get Main_Window to hoMain
        Send Adjust_Logicals of hoMain

        Set Location to 0 0
        Get Size of hoMain to iPanelSize
        Get Window_Handle of hoMain to hWnd
        If (DR_WinAPI_IsZoomed (hWnd)) Begin
            Set Size to (Hi (iPanelSize) - 75) (Low (iPanelSize) - 75)
        End
        Else Begin
            Set Size to (Hi (iPanelSize)) (Low (iPanelSize))
        End

        Get Location of hoMain to iLocation
        Set Location to (Hi (iLocation) + 25) (Low (iLocation) + 25)
    End_Procedure

    { Visibility = Private }
	Procedure UpdateViewer
        Handle hoReport hoClient hWnd

        Get phoReport to hoReport
        If (hoReport <> 0) Begin
            Send OnShowPreviewer of hoReport
        End

        Send AssignPreviewHWND

        Get Client_Id to hoClient
        If (hoClient <> 0) Begin
            Get Window_Handle of hoClient to hWnd
            If (hWnd <> 0) Begin
                Move (UpdateWindow (hWnd)) to hWnd
            End
        End
    End_Procedure

    // Close the report when it the dialog is closed. Destroy the dialog.
    { Visibility = Private }
    Procedure Close_Panel
        Handle hoReport

        Get phoReport to hoReport
        Get Object_Id of hoReport to hoReport // Check if this is still a valid handle
        If (hoReport <> 0) Begin
            Send OnClosePreviewer of hoReport
            Set phoPreviewContainer of hoReport to 0
            Send CancelReport of hoReport
            Set PreviewWindowHandle of hoReport to 0 // reset
        End

        Forward Send Close_Panel
    End_Procedure
End_Class
